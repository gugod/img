#!/usr/bin/env perl
use v5.18;
use File::Path qw< make_path >;
use Getopt::Long qw< GetOptions >;
use Parallel::ForkManager;

use FindBin;
use lib "${FindBin::Bin}/../lib";

use Img;

sub main {
    my ($opts, $image_file, $input, $output_dir) = @_;
    make_path($output_dir) unless -d $output_dir;
    die unless -f $input;

    open my $fh, "<", $input;
    my @bbox;
    while (my $k = <$fh>) {
        chomp($k);
        my @b = split /,/, $k;
        push @bbox, { k => $k, top => $b[0], bottom => $b[1], left => $b[2], right => $b[3] };
    }
    close($fh);

    my $img = Img->new( file => $image_file ) or die Img->errstr;
    my $forkman = Parallel::ForkManager->new(2);
    for my $bbox (@bbox) {
        my $k = delete $bbox->{k};
        my $out = "$output_dir/$k.png";
        next if -f $out;

        $forkman->start and next;
        my $img_box = $img->crop(%$bbox);
        unless ($img_box->looks_boring) {
            $img_box->write(file => $out);
        }
        $forkman->finish;
    }
    $forkman->wait_all_children;
}

my $opts = {};
die if (@ARGV != 3);
main($opts, @ARGV);
