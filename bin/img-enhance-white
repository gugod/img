#!/usr/bin/env perl
use v5.14;
use strict;
use warnings;
use Imager;
use Imager::Color;

use File::Basename qw(basename);

@ARGV == 2 or die <<"USAGE";

Usage:

    @{[ basename($0) ]} input_image_file_name output_image_file_name

USAGE

my ($input_image_file, $output_image_file_name) = @ARGV;

my $THRESHOLD = 255 * 0.7;

my $white = Imager::Color->new("#FFFFFF");
my $black = Imager::Color->new("#000000");
my $img = Imager->new( file => $input_image_file );
my %color_count;

for my $y ( 0 .. $img->getheight - 1 ) {
    my @colors = $img->getscanline(y => $y);
    my $dirty = 0;
    for (my $i = 0; $i < @colors; $i++) {
        my @rgba = $colors[$i]->rgba;
        if ($rgba[0] > $THRESHOLD && $rgba[1] > $THRESHOLD && $rgba[2] > $THRESHOLD) {
            $colors[$i] = $white;
            $dirty = 1;
        }
        if ($rgba[0] < 128 && $rgba[1] < 128  && $rgba[2] < 128) {
            $colors[$i] = $black;
            $dirty = 1;
        }
    }
    $img->setscanline(y => $y, pixels => \@colors) if $dirty;
}

$img->write( file => $output_image_file_name )
    or die $img->errstr;
